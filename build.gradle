plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group 'de.geolykt'
def archivesBaseName = 'starloader'
version '2.1.1'

// Our classloader require Java9+ in order to work, but since noone should be using Java 9 and 10, it was bumped all the way to Java 11
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '11'

repositories {
    mavenCentral()
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'Geolykt'
        url = 'https://geolykt.de/maven/'
    }
}

dependencies {

    // I am very well aware that this is just a reference implementation, but it has served me well in the past; so I prefer it over any other implementation
    api 'org.json:json:20210307'

    // Let's use a library that's over 2 and a half decades old because why not?
    // The lack of maintainance hopefully is not too harming
    // https://mvnrepository.com/artifact/tablelayout/TableLayout
    api group: 'tablelayout', name: 'TableLayout', version: '20050920'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    api group: 'org.jetbrains', name: 'annotations', version: '22.0.0'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    api 'com.google.code.gson:gson:2.8.8'

    // Required for mixins
    api 'com.google.guava:guava:30.1.1-jre'

    // Code modification
    api "org.ow2.asm:asm:9.2"
    api "org.ow2.asm:asm-tree:9.2"
    api "org.ow2.asm:asm-analysis:9.2"
    api "org.ow2.asm:asm-util:9.2"
    api "org.ow2.asm:asm-commons:9.2"
    api "org.spongepowered:mixin:0.8.4-SNAPSHOT"
    api "de.geolykt.starloader:access-widener:2.0.0"

    // Logging
    // SLF4J is the base logger for most libraries, therefore we should hook it into log4j2.
    api 'org.apache.logging.log4j:log4j-core:2.14.1'
    api 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'de.geolykt.starloader.launcher.Launcher', 'Multi-Release': true
    }
}

shadowJar {
    mergeServiceFiles()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version '2.1.1'

            from components['java']

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
    }
}
