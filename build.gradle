plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'maven-publish'
    id "eclipse"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group 'de.geolykt.starloader'
def archivesBaseName = 'launcher'
version '4.0.0-20230218'

// Our classloader requires Java 9+ in order to work, but since noone should be using Java 9 and 10, it was bumped all the way to Java 11
// Java 16/17 was not used because it is not needed and would unnecessarily lower the amount of users that could use the application
// out of the box.
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '11'

repositories {
    mavenLocal() // Required for Micromixin - we may want to use a proper library in the future
    mavenCentral()
}

sourceSets {
    bootstrap {
        ext.javaVersion = 8
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

java {
    modularity.inferModulePath.set(true)
}

dependencies {

    // I am very well aware that this is just a reference implementation, but it has served me well in the past; so I prefer it over any other implementation
    // https://mvnrepository.com/artifact/org.json/json
    api 'org.json:json:20220924'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    api group: 'org.jetbrains', name: 'annotations', version: '24.0.0'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    api 'com.google.code.gson:gson:2.8.9'

    // Code modification
    api "org.ow2.asm:asm:9.4"
    api "org.ow2.asm:asm-tree:9.4"
    api "org.ow2.asm:asm-analysis:9.4"
    api "org.ow2.asm:asm-util:9.4"
    api "org.ow2.asm:asm-commons:9.4"
    implementation "de.geolykt.starloader:micromixin-transformer:0.0.1-SNAPSHOT"

    // Logging
    // SLF4J is the base logger for most libraries, therefore we should provide integration to it
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    api 'org.slf4j:slf4j-api:2.0.6'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'ch.qos.logback:logback-core:1.4.5'
}

jar {
    manifest {
        attributes 'Multi-Release': true
    }
}

shadowJar {
    mergeServiceFiles()
    from sourceSets.main.output, sourceSets.bootstrap.output
}

shadowJar.dependsOn(bootstrapClasses)

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource, sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId 'launcher'
            version project.version

            from components['java']

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
        /*maven {
          url = "/home/geolykt/.m2/alt/maven/"
        }*/
    }
}

project.sourceSets.each { set -> {
    if (set.ext.has("javaVersion")) {
        project.tasks[set.compileJavaTaskName].sourceCompatibility = set.ext.javaVersion
        project.tasks[set.compileJavaTaskName].targetCompatibility = set.ext.javaVersion
        project.tasks[set.compileJavaTaskName].javaCompiler = javaToolchains.compilerFor {
            languageVersion = JavaLanguageVersion.of(set.ext.javaVersion)
        }
    }
}}
